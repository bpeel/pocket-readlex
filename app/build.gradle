apply plugin: 'com.android.application'

android {
    namespace "uk.co.busydoingnothing.pocketrl"

    compileSdkVersion 28

    defaultConfig {
        applicationId "uk.co.busydoingnothing.pocketrl"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "0.1"
    }

   signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach {
        def variant = name.capitalize()
        def dependentTasks = ["merge${variant}Assets",
                              "generate${variant}LintVitalReportModel",
                              "lintVitalAnalyze${variant}"]
        dependentTasks.each { t ->
            def task = tasks.findByName(t)
            if (task) {
                task.dependsOn(buildDictionary)
            }
        }
    }

    externalNativeBuild {
        cmake {
            path= "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
}

task buildDictionary {
    outputs.file 'src/main/assets/dictionary.bin'
    outputs.dir 'src/main/assets/articles'

    doLast {
        exec {
            commandLine 'cargo', 'run', '--release', '--manifest-path', '../compiledb/Cargo.toml', '--', '-i', '../extern/readlex/readlex.json', '-o', 'src/main/assets/dictionary.bin', '-a', 'src/main/assets/articles'
        }
    }
}
